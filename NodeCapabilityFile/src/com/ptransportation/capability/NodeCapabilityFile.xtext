grammar com.ptransportation.capability.NodeCapabilityFile with org.eclipse.xtext.common.Terminals
// org.eclipse.xtext.xbase.Xbase

generate nodeCapabilityFile "http://www.ptransportation.com/capability/NodeCapabilityFile"

NodeCapabilityFile:
	'node_capability_file' ';'
	'LIN_language_version' '=' languageVersion=STRING ';'
	node=Node;

Node: Slave | Master;

Slave:
	'node' name=ID '{'
		'general' '{'
			'LIN_protocol_version' '=' protocolVersion=STRING ';'
			'supplier' '=' supplier=Integer ';'
			'function' '=' function=Integer ';'
			'variant' '=' variant=Integer ';'
			'bitrate' '=' bitrate=Bitrate ';'
			'sends_wake_up_signal' '=' sendsWakeUpSignal=('"yes"' | '"no"') ';'
		'}'

		'diagnostic' '{'
            'NAD' '=' nadSet=NadSet ';'
            'diagnostic_class' '=' diagnosticClass=Integer ';'
            ('P2_min' '=' p2Min=Number 'ms' ';')?
            ('ST_min' '=' stMin=Number 'ms' ';')?
            ('N_As_timeout' '=' nAsTimeout=Number 'ms' ';')?
            ('N_Cr_timeout' '=' nCrTimeout=Number 'ms' ';')?
            ('support_sid' '{' supportedSIDS+=Integer (',' supportedSIDS+=Integer)* '}')?
            ('max_message_length' '=' maxMessageLength=Integer)?
		'}'

		'frames' '{'
		    (frames+=Frame)+
		'}'

		'encoding' '{'
		    (encodings+=Encoding)*
		'}'

		'status_management' '{'
		    'response_error' '=' responseError=[Signal] ';'
		    ('fault_state_signals' '=' faultStateSignals+=[Signal] (',' faultStateSignals+=[Signal])* ';')?
		'}'

		('free_text' '{'
			freeText=STRING
		'}')?
	'}';

Master:
	'master' name=ID '{'
		'general' '{'
			'LIN_protocol_version' '=' protocolVersion=STRING ';'
			'supplier' '=' supplier=Integer ';'
			'function' '=' function=Integer ';'
			'variant' '=' variant=Integer ';'
			'bitrate' '=' bitrate=FixedBitrate ';'
			'timebase' '=' timebase=Number 'ms' ';'
			'jitter' '=' jitter=Number 'ms' ';'
		'}'

		'slaves' '{'
			(slaves+=[Slave] ';')+
		'}'

		'frames' '{'
			(frames+=Frame)*
		'}'

		'encoding' '{'
			(encodings+=Encoding)*
		'}'

		'schedule_tables' '{'
			(scheduleTables+=ScheduleTable)*
		'}'

		('free_text' '{'
			freeText=STRING
		'}')?
	'}';

Bitrate: SelectBitrate | FixedBitrate | AutomaticBitrate;

AutomaticBitrate:
	{AutomaticBitrate} 'automatic' ('min' minValue=Number 'kbps')? ('max' maxValue=Number 'kbps')? ;

SelectBitrate:
	'select' '{' values+=Number 'kbps' (',' values+=Number 'kbps')* '}'
;

FixedBitrate:
	value=Number 'kbps';

NadSet: NadList | NadRange;

NadList: values+=Integer (',' values+=Integer)*;

NadRange: minValue=Integer 'to' maxValue=Integer;

Frame: 
	(publishes='publish' | subscribes='subscribe') name=ID '{'
		'length' '=' length=Integer ';'
		('min_period' '=' minPeriod=Integer 'ms' ';')?
		('max_period' '=' maxPeriod=Integer 'ms' ';')?
		('event_triggered_frame' '=' eventTriggeredFrame=[Frame] ';')?
		('signals' '{'
			(signals+=Signal)+
		'}')?
	'}';

Signal:
	name=ID '{'
		'size' '=' size=Integer ';'
		'init_value' '=' initialValue=(ScalorSignalValue | ArraySignalValue) ';'
		'offset' '=' offset=Integer ';'
		(encoding=[Encoding] ';')?
	'}';

ScalorSignalValue: value=Integer;
ArraySignalValue: '{' values+=Integer (',' values+=Integer)* '}';

Encoding:
	name=ID '{'
	    (encodedValues+=EncodedValue)+
	'}';

EncodedValue:
	LogicalEncodedValue ';' |
	PhysicalEncodedRange ';' |
	BCDEncodedValue ';' |
	ASCIIEncodedValue ';'
;

LogicalEncodedValue:
	'logical_value' ',' value=Integer (',' textInfo=STRING)?
;

PhysicalEncodedRange:
	'physical_value' ',' minValue=Integer ',' maxValue=Integer ',' scale=Number ',' offset=Number (',' textInfo=STRING)?
;

BCDEncodedValue:
	{BCDEncodedValue} 'bcd_value'
;

ASCIIEncodedValue:
	{ASCIIEncodedValue} 'ascii_value'
;

ScheduleTable:
	name=ID '{'
		(entries+=ScheduleTableEntry)+
	'}';

ScheduleTableEntry:
	(FrameEntry |
	MasterReqEntry |
	SlaveRespEntry |
	AssignNADEntry |
	ConditionalChangeNADEntry |
	DataDumpEntry |
	SaveConfigurationEntry |
	AssignFrameIdRangeEntry |
	FreeFormatEntry |
	AssignFrameIdEntry) 'delay' frameTime=Number 'ms' ';'
;

FrameEntry: frame=[Frame];

MasterReqEntry:
	{MasterReqEntry} 'MasterReq';

SlaveRespEntry:
	{SlaveRespEntry} 'SlaveResp';

AssignNADEntry:
	'AssignNAD' '{' node=[Node] '}';

ConditionalChangeNADEntry:
	'ConditionalChangeNAD' '{'
		NAD=Integer ',' id=Integer ',' byte_=Integer',' mask=Integer',' inv=Integer',' newNAD=Integer
	'}';

DataDumpEntry:
	'DataDump' '{'
		node=[Node] ',' d1=Integer',' d2=Integer',' d3=Integer',' d4=Integer',' d5=Integer
	'}';

SaveConfigurationEntry:
	'SaveConfiguration' '{' node=[Node] '}';

AssignFrameIdRangeEntry:
	'AssignFrameIdRange' '{'
		node=[Node]',' frameIndex=Integer (',' frame0_PID=Integer',' frame1_PID=Integer',' frame2_PID=Integer',' frame3_PID=Integer)?
	'}';

FreeFormatEntry:
	'FreeFormat' '{'
		d1=Integer',' d2=Integer',' d3=Integer',' d4=Integer',' d5=Integer',' d6=Integer',' d7=Integer',' d8=Integer
	'}';

AssignFrameIdEntry:
	'AssignFrameId' '{' node=[Node] ',' frame=[Frame] '}';

Number:
	INT ('.' INT)?;

Integer: INT | HEX;

terminal HEX:
	('0x'|'0X') ('0'..'9'|'a'..'f'|'A'..'F')+;
